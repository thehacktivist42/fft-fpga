`timescale 1ns/1ps

`include "submodules/stage.sv"
`include "submodules/stage_trivial.sv"

module fft_top #(
    parameter WIDTH = 16,
    parameter IN_WIDTH = 32,
    parameter TWIDDLE_WIDTH = 16
)(
    input  wire clk,
    input  wire rst_n,
    input  wire signed [IN_WIDTH-1:0] in_real,
    input  wire signed [IN_WIDTH-1:0] in_imag,
    input  wire [$clog2(WIDTH)-1:0] sample_count,

    output wire signed [IN_WIDTH-1:0] out_real,
    output wire signed [IN_WIDTH-1:0] out_imag,
    output wire [$clog2(WIDTH) - 1:0] out_sample_count
);

    //localparameters
    localparam NUM_STAGES = $clog2(WIDTH);
    localparam QUARTER_WIDTH = (WIDTH >= 4) ? WIDTH/4 : 1;

    // Arrays to interconnect the data and control signals
    wire signed [IN_WIDTH-1:0] stage_real [0:NUM_STAGES];
    wire signed [IN_WIDTH-1:0] stage_imag [0:NUM_STAGES];
    wire [$clog2(WIDTH)-1:0]  stage_count [0:NUM_STAGES];

    assign stage_real[0] = in_real;
    assign stage_imag[0] = in_imag;
    assign stage_count[0] = sample_count;

    // ROM arrays for twiddle factors
    (* ram_style="distributed" *) reg signed [TWIDDLE_WIDTH-1:0] rom_real [0:QUARTER_WIDTH-1];
    (* ram_style="distributed" *) reg signed [TWIDDLE_WIDTH-1:0] rom_imag [0:QUARTER_WIDTH-1];

    generate
        if (WIDTH >= 8) begin : gen_rom_init
            initial begin
                $readmemh("twiddles_real.hex", rom_real);
                $readmemh("twiddles_imag.hex", rom_imag);
            end
        end else begin : gen_rom_tieoff
            initial begin
                rom_real[0] = '0;
                rom_imag[0] = '0;
            end
        end
    endgenerate

    genvar i;
    generate
        for (i = 1; i <= NUM_STAGES; i = i + 1) begin : gen_fft_stages
            if (WIDTH == 2 || (WIDTH == 4 && i <= 2)) begin: gen_trivial_stage
                stage_trivial #(
                    .WIDTH(WIDTH), 
                    .IN_WIDTH(IN_WIDTH), 
                    .STAGE(i)
                ) stg_inst_trivial (
                    .clk(clk), 
                    .rst_n(rst_n),
                    .in_real(stage_real[i-1]), 
                    .in_imag(stage_imag[i-1]), 
                    .sample_count(stage_count[i-1]),
                    .out_real(stage_real[i]), 
                    .out_imag(stage_imag[i])
                );
            end
            else begin: gen_complex_stage
                stage #(
                    .WIDTH(WIDTH), 
                    .IN_WIDTH(IN_WIDTH), 
                    .TWIDDLE_WIDTH(TWIDDLE_WIDTH), 
                    .STAGE(i)
                ) stg_inst (
                    .clk(clk), 
                    .rst_n(rst_n),
                    .in_real(stage_real[i-1]), 
                    .in_imag(stage_imag[i-1]), 
                    .sample_count(stage_count[i-1]), 
                    .rom_imag(rom_imag), 
                    .rom_real(rom_real),
                    .out_real(stage_real[i]), 
                    .out_imag(stage_imag[i])
                );
            end

            begin : gen_delay
                // Delay = (Buffer Depth of current stage) + 4 cycles
                localparam DELAY_DEPTH = (WIDTH >> i) + 4; 
                
                reg [$clog2(WIDTH)-1:0] delay_pipe [0:DELAY_DEPTH-1];
                
                // Reset removed to allow Vivado to infer efficient SRL primitives
                always_ff @(posedge clk) begin
                    delay_pipe[0] <= stage_count[i-1];
                    for (int j = 1; j < DELAY_DEPTH; j++) begin
                        delay_pipe[j] <= delay_pipe[j-1];
                    end
                end
                assign stage_count[i] = delay_pipe[DELAY_DEPTH-1];
            end
        end
    endgenerate

    assign out_real = stage_real[NUM_STAGES];
    assign out_imag = stage_imag[NUM_STAGES];
    assign out_sample_count = stage_count[NUM_STAGES];

endmodule